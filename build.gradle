/*
 * Copyright (c) 2015-2021, Virgil Security, Inc.
 *
 * Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>
 *
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     (1) Redistributions of source code must retain the above copyright notice, this
 *     list of conditions and the following disclaimer.
 *
 *     (2) Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *
 *     (3) Neither the name of virgil nor the names of its
 *     contributors may be used to endorse or promote products derived from
 *     this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

buildscript {
    ext.versions = [
            // Virgil
            virgilSdk       : '7.4.0',
            virgilCrypto    : '0.17.2',
            pythia          : '0.4.0', // With virgilCrypto 0.17.2
            ratchet         : '0.3.0', // With virgilCrypto 0.17.2

            // Kotlin
            kotlin          : '1.9.24',
            coroutines      : '1.3.3',

            // Gradle
            agp             : '8.5.2',
            gradle          : '8.7',

            // Android
            appCompat       : '1.1.0',

            // Room
            room            : '2.2.3',

            // Common
            gson            : '2.10.1',

            // Docs
            dokka           : '1.9.20',

            // Tests
            junit           : '4.12',
            testsRunner     : '1.5.2',
            testsExtJunit   : '1.1.5',
            testsRules      : '1.5.0',
            espresso        : '3.0.2',
            virgilTestCommon: '0.1',

            // Benchmark
            androidBenchmark: '1.0.0',
    ]
    ext.androidOptions = [
            compileSdkVersion   : 34,
            minSdkVersionEnclave: 23,
            minSdkVersionRegular: 21,
            targetSdkVersion    : 34,
    ]
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$versions.agp"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$versions.dokka"
        classpath "androidx.benchmark:benchmark-gradle-plugin:$versions.androidBenchmark"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        // Legacy artifacts (e.g. Fuel 1.15.1) required by ratchet/pythia are no longer on Maven Central.
        maven {
            url "https://maven.aliyun.com/repository/public"
        }
        maven {
            url "https://jitpack.io"
        }
        maven {
            url "https://jcenter.bintray.com/"
        }
        maven {
            url "https://central.sonatype.com/repository/maven-snapshots/"
        }
    }

    configurations.configureEach {
        resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.virgilsecurity.sdk') {
                details.useVersion(versions.virgilSdk)
                details.because("Pin Virgil SDK artifacts to ${versions.virgilSdk}")
            }
        }
    }
}

/**
 * Gets property from system properties.
 *
 * @param name Of System property to get.
 *
 * @return System property or null if not found.
 */
def static getSystemProperty(String name) {
    def property
    if (System.getProperty(name) != null) {
        property = System.getProperty(name)
    } else {
        property = System.getenv(name)
    }

    return property
}

/**
 * Gets property from both - gradle properties and system properties.
 *
 * @param name Of property to get.
 */
def getGradleOrSystemProperty(String name, Project project) {
    def property

    if (project.hasProperty(name)) {
        property = project.getProperty(name)
    } else {
        property = getSystemProperty(name)
    }

    return property
}

// Artifacts packages
final String BASE_VIRGIL_PACKAGE = 'com.virgilsecurity'

// Packages versions
final String SDK_VERSION = '2.1.0-rc1'

final Map<String, String> namespaces = [
        'ethree-common'   : 'com.virgilsecurity.android.common',
        'ethree-kotlin'   : 'com.virgilsecurity.android.ethree.interaction',
        'ethree-enclave'  : 'com.virgilsecurity.android.ethreeenclave',
        'tests'           : 'com.virgilsecurity.android.ethree',
        'testsenclave'    : 'com.virgilsecurity.android.testsenclave',
        'ethree-benchmark': 'com.virgilsecurity.android.ethree_benchmark',
]

subprojects {
    def moduleName = project.name
    group BASE_VIRGIL_PACKAGE
    version SDK_VERSION

    def isRegular = (moduleName == 'ethree-common'
            || moduleName == 'ethree-kotlin'
            || moduleName == 'ethree-enclave')
    def isTest = (moduleName == 'tests' || moduleName == 'testsenclave')
    def isBenchmark = (moduleName == 'ethree-benchmark')

    if (isRegular) {
        apply plugin: 'com.android.library'
        apply plugin: 'org.jetbrains.dokka'
        apply plugin: 'signing'
        apply plugin: 'maven-publish'
    } else if (isTest) {
        apply plugin: 'com.android.application'
        apply from: '../tests-verbal-output.gradle'
    } else if (isBenchmark) {
        apply plugin: 'com.android.library'
        apply plugin: 'androidx.benchmark'
    }

    apply plugin: 'kotlin-android'

    if (moduleName == 'ethree-common') {
        apply from: '../tests-verbal-output.gradle'
        apply plugin: 'kotlin-kapt'
    }

    android {
        namespace namespaces[moduleName]
        compileSdk androidOptions.compileSdkVersion

        defaultConfig {
            targetSdk androidOptions.targetSdkVersion
        }

        if (isRegular || isBenchmark) {
            defaultConfig {
                minSdk androidOptions.minSdkVersionRegular
            }
        }

        buildTypes {
            debug {
                minifyEnabled false
            }
            release {
                minifyEnabled false
            }
        }

        lint {
            abortOnError false
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_8
            targetCompatibility JavaVersion.VERSION_1_8
        }

        if (isRegular) {
            publishing {
                singleVariant('release') {
                    withSourcesJar()
                }
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = '1.8'
        }
    }

    if (isRegular) {
        // Central bundle upload uses Maven repository layout; Gradle module metadata is unnecessary.
        tasks.withType(org.gradle.api.publish.tasks.GenerateModuleMetadata).configureEach {
            enabled = false
        }

        task javadocJar(type: Jar, dependsOn: 'dokkaHtml') {
            from("$buildDir/dokka/html")
            archiveClassifier = 'javadoc'
        }

        publishing {
            publications {
                mavenJava(MavenPublication) {
                    artifact javadocJar

                    afterEvaluate {
                        from components.release
                    }

                    pom {
                        description = 'Virgil Security provides an SDK which symplifies work with Virgil services and presents easy to use API for adding security to any application. In a few simple steps you can setup user encryption with multidevice support.'
                        url = 'https://www.virgilsecurity.com/'
                        licenses {
                            license {
                                name = 'Virgil Security, Inc. license'
                                url = 'https://github.com/VirgilSecurity/virgil-e3kit-kotlin/blob/master/LICENSE.md'
                            }
                        }
                        developers {
                            developer {
                                id = 'virgil-security-inc'
                                name = 'Virgil Security, Inc.'
                                email = 'support@virgilsecurity.com'
                                organizationUrl = 'https://www.virgilsecurity.com/'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/VirgilSecurity/virgil-e3kit-kotlin.git'
                            developerConnection = 'scm:git:git@github.com:VirgilSecurity/virgil-e3kit-kotlin.git'
                            url = 'https://github.com/VirgilSecurity/virgil-e3kit-kotlin'
                        }
                    }
                }
            }

            repositories {
                maven {
                    name = 'centralBundle'
                    url = uri("${rootProject.buildDir}/central-bundle-repo")
                }
            }
        }

        signing {
            def signingKey = getGradleOrSystemProperty('signingKey', project)
            def signingPassword = getGradleOrSystemProperty('signingPassword', project)

            if (signingKey != null && signingPassword != null) {
                useInMemoryPgpKeys(signingKey, signingPassword)
            }

            sign publishing.publications.mavenJava
        }

        tasks.withType(org.jetbrains.dokka.gradle.DokkaTask).configureEach {
            dokkaSourceSets.configureEach {
                reportUndocumented.set(false)
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
